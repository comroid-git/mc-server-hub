plugins {
    //id 'war'
    id 'application'
    id 'org.springframework.boot' version '3.0.+'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
}

group = 'org.comroid.mcsd'
version = '0.1.0-SNAPSHOT'
sourceCompatibility = '17'
targetCompatibility = '17'
mainClassName = 'org.comroid.mcsd.hub.Program'

tasks.register('simplify', Copy) {
    dependsOn 'bootJar'
    from bootJar.outputs.files.singleFile
    into "$buildDir/libs"
    rename { "${project.name}.jar" }
    doLast { System.out.println("Boot Jar saved as $buildDir/libs/${project.name}.jar") }
}

sourceSets {
    main.java.srcDirs       = files(parent.projectDir.toString() + "/src/hub/main/java")
    main.resources.srcDirs  = files(parent.projectDir.toString() + "/src/hub/main/resources")
    test.java.srcDirs       = files(parent.projectDir.toString() + "/src/hub/test/java")
    test.resources.srcDirs  = files(parent.projectDir.toString() + "/src/hub/test/java")
}

bootWar {
    webAppDirectory = file(parent.projectDir.toString() + "/src/hub/main/webapp")
}

repositories {
    maven { url 'https://maven.comroid.org' }
    mavenLocal()
    jcenter()
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation project(':core')

    // db
    implementation 'com.mysql:mysql-connector-j'

    // data
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    // web
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    implementation 'org.springframework:spring-messaging:6.0.0'

    // security
    implementation 'org.springframework.session:spring-session-core'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

    // ssh
    implementation 'org.apache.sshd:sshd-core:2.+'
    implementation 'org.apache.sshd:sshd-sftp:2.+'

    // minecraft
    implementation 'io.github.fragland:MineStat:3.+'
    //implementation 'com.github.jaqobb:namemc-api:master-SNAPSHOT'
    implementation 'io.github.MrGraversen:minecraft-rcon:0.+'

    // webjars
    implementation 'org.webjars:webjars-locator-core'
    implementation 'org.webjars:sockjs-client:1.+'
    implementation 'org.webjars:stomp-websocket:2.+'
    implementation 'org.webjars:bootstrap:3.+'
    implementation 'org.webjars:jquery:3.+'

    // utility
    developmentOnly 'org.springframework.boot:spring-boot-starter-actuator'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'io.projectreactor:reactor-test'
}
