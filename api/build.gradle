plugins {
    id 'java-library'
    id 'maven-publish'
}

group = 'org.comroid.mcsd'
version = '0.1.0-SNAPSHOT'
sourceCompatibility = '17'
targetCompatibility = '17'

ext {
    userbase = 'comroid-git'
    projectDescription = 'Minecraft Server Hub API'
}

sourceSets {
    main.java.srcDirs       = files(parent.projectDir.toString() + "/src/api/main/java")
    main.resources.srcDirs  = files(parent.projectDir.toString() + "/src/api/main/resources")
    test.java.srcDirs       = files(parent.projectDir.toString() + "/src/api/test/java")
    test.resources.srcDirs  = files(parent.projectDir.toString() + "/src/api/test/java")
}

repositories {
    maven { url 'https://maven.comroid.org' }
    mavenLocal()
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

dependencies {
    // json
    api 'com.fasterxml.jackson.core:jackson-databind:2.14.+'

    // jpa
    implementation 'jakarta.persistence:jakarta.persistence-api:2.+'

    // utility
    compileOnly 'org.projectlombok:lombok:+'
    annotationProcessor 'org.projectlombok:lombok:+'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava

    try {
        archiveClassifier.set 'sources'
    } catch (MissingPropertyException ignored) {
        classifier = "sources"
    }
}

task javadocJar(type: Jar) {
    from javadoc.destinationDir

    try {
        archiveClassifier.set 'javadoc'
    } catch (MissingPropertyException ignored) {
        classifier = "javadoc"
    }
}

publishing {
    repositories {
        mavenLocal()
    }
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'mcsd-api'
            from components.java

            artifact sourcesJar
            artifact javadocJar

            pom {
                name = rootProject.name
                description = project.projectDescription
                inceptionYear = '2020'
                url = "https://github.com/$userbase/$rootProject.name".toString()

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'kaleidox'
                        name = 'Tobias Burdow'
                        email = 'contact@comroid.org'
                    }
                }

                scm {
                    connection = "scm:git:git://github.com/$userbase/${rootProject.name}.git".toString()
                    developerConnection = "scm:git:ssh://github.com/$userbase/${rootProject.name}.git".toString()
                    url = "https://github.com/$userbase/$rootProject.name".toString()
                }
            }

            System.out.println("Registered artifact publication: '${group}:${artifactId}:${project.version}' -> $project.projectDescription")
        }
    }
}
