plugins {
    id 'war'
    id 'java'
    id 'org.springframework.boot' version '3.0.+'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
}

group = 'org.comroid'
version = '0.1.0-SNAPSHOT'
sourceCompatibility = '17'
targetCompatibility = '17'

tasks.register('simplifyDist', Copy) {
    dependsOn 'bootWar'
    from bootWar.outputs.files.singleFile
    def target = file("$buildDir/dist")
    into target
    rename { String filename -> return "mc-server-hub.war" }
}

sourceSets {
    main.java.srcDirs       = files(parent.projectDir.toString() + "/src/webui/main/java")
    main.resources.srcDirs  = files(parent.projectDir.toString() + "/src/webui/main/resources")
    test.java.srcDirs       = files(parent.projectDir.toString() + "/src/webui/test/java")
    test.resources.srcDirs  = files(parent.projectDir.toString() + "/src/webui/test/java")
}

war {
    webAppDirectory = file(parent.projectDir.toString() + "/src/webui/main/webapp")
}

repositories {
    maven { url 'https://maven.comroid.org' }
    mavenLocal()
    jcenter()
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation project(':core')

    if (findProject(':japi') != null)
        implementation project(':japi')
    else implementation 'org.comroid:api:+'

    // db
    implementation 'com.mysql:mysql-connector-j'

    // data
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    // web
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    implementation 'org.springframework:spring-messaging:6.0.0'

    // jstl
    compileOnly 'jstl:jstl:1.2'
    implementation 'org.glassfish.web:jakarta.servlet.jsp.jstl'
    implementation 'jakarta.servlet.jsp.jstl:jakarta.servlet.jsp.jstl-api'
    providedRuntime 'org.apache.tomcat.embed:tomcat-embed-jasper'
    providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'

    // security
    implementation 'org.springframework.session:spring-session-core'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'

    // ssh
    implementation 'org.apache.sshd:sshd-core:2.+'
    implementation 'org.apache.sshd:sshd-sftp:2.+'

    // minecraft
    implementation 'io.github.fragland:MineStat:3.+'
    //implementation 'com.github.jaqobb:namemc-api:master-SNAPSHOT'
    implementation 'io.github.MrGraversen:minecraft-rcon:0.+'

    // webjars
    implementation 'org.webjars:webjars-locator-core'
    implementation 'org.webjars:sockjs-client:1.+'
    implementation 'org.webjars:stomp-websocket:2.+'
    implementation 'org.webjars:bootstrap:3.+'
    implementation 'org.webjars:jquery:3.+'

    // utility
    developmentOnly 'org.springframework.boot:spring-boot-starter-actuator'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'io.projectreactor:reactor-test'
}
